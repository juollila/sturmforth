HEX CR
.( SIMPLE LINE EDITOR ) CR
.( COMMANDS: ) CR
.( N1 N2 COPY - COPY LINE N1 TO N2 ) CR
.( N DEL - DELETE LINE N ) CR
.( N1 N2 LIST - LIST LINES N1-N2 ) CR
.( N PUT TEXT - INSERT TEXT TO LINE N ) CR
.( LOAD FILE - READ A FILE ) CR
.( N1 N2 SAVE FILE - WRITE LINES TO A FILE ) CR
CREATE FILENAME 40 ALLOT
C000 CONSTANT TEXT    40 CONSTANT MAXLINE
40 CONSTANT LINELEN   0D CONSTANT EOL
TEXT 1000 EOL FILL    01 CONSTANT FILEID
40 CONSTANT EOF
: LINE ( N -- ADDR ) 1- LINELEN * TEXT + ;
: LOWBYTE ( N -- BYTE ) 255 AND ;
: HIGHBYTE ( N -- BYTE ) 8 0 DO 2/ LOOP ;
: CLEAN ( -- ) 2DROP 2DROP ;
: COPY ( N N -- ) LINE SWAP LINE SWAP LINELEN CMOVE ;
: DEL ( N -- ) LINE EOL SWAP C! ;
: READSTRING ( -- ADDR N ) EOL WORD DUP C@ SWAP 2+ SWAP ;
: PUT ( N -- ) LINE READSTRING ROT SWAP CMOVE ;
: PRINTLINE ( N -- )
  LINE LINELEN 0 DO
    DUP I + C@ DUP EOL = IF DROP LEAVE ELSE EMIT THEN
  LOOP DROP CR
;
: LIST ( N N -- )
  CR 1+ SWAP DO I DUP DUP .
  10 < IF SPACE THEN
  PRINTLINE LOOP
;
: STATUS ( N -- ) 0 > IF ." I/O OPERATION FAILED" CR THEN ;
: LOAD
  READSTRING 1- FILEID OPEN-FILE STATUS
  MAXLINE 1 DO
    I LINE LINELEN FILEID READ-LINE
    SWAP I LINE + EOL SWAP C!
    EOF = IF LEAVE THEN
  LOOP FILEID CLOSE-FILE DROP
;
: LEN ( N -- N )
  LINE LINELEN 0 DO
    DUP I SWAP OVER + C@ EOL = IF LEAVE ELSE DROP THEN
  LOOP SWAP DROP 1+
;
: SAVE
  READSTRING 1- 2DUP + DUP
  2C SWAP C!  1+ 57 SWAP C! ( ,W )
  2+ FILEID OPEN-FILE STATUS
  1+ SWAP DO
    I DUP LINE SWAP LEN FILEID WRITE-FILE DROP
  LOOP FILEID CLOSE-FILE DROP
;
DECIMAL
